buildscript {
    ext.kotlin_version = '1.0.0'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.7"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.6"
}

apply plugin: 'kotlin'

group 'net.serverpeon'
version '1.0-alpha2-SNAPSHOT'

allprojects {
    repositories {
        mavenCentral()
        jcenter()
    }

    apply plugin: 'maven-publish'
}

dependencies {
    compile project(":implementation")
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.4'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.4'
}

publishing {
    publications {
        // Import sub-project publications
        subprojects.forEach { project ->
            //noinspection GrUnresolvedAccess,GroovyAssignabilityCheck
            addAll(project.publishing.publications)
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['api', 'implementation']
    pkg {
        repo = 'maven'
        name = 'discordkt'
        userOrg = user
        licenses = ["MIT"]
        vcsUrl = "https://github.com/Kiskae/DiscordKt.git"
        githubRepo = "Kiskae/DiscordKt"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}